namespace LALR {
namespace DPDA {

class Interpreter {
    var dpda;
    var input;
    var stack;
    var state;
    var error_occurred;

    function init[vtable]() {
	self.input = [];
	self.stack = [];
	self.error_occurred = 0;
    }

    function BUILD(dpda[named]) {
	self.dpda = dpda;
	self.state = dpda.start();
    }

    function state() {
	return self.state;
    }

    function error_occurred() {
	return self.error_occurred;
    }

    function feed_input(string symbol) {
	${ push self.input, symbol };
    }

    function perform_transition() {
	var transitions = self.dpda.transitions_from(self.state);
	if (transitions[0] instanceof LALR.DPDA.ReadTransition) {
	    if (!self.input) {
		throw "Unexpected end of input.";
	    }
	    for (var transition in transitions) {
		if (!(transition instanceof LALR.DPDA.ReadTransition)) {
		    throw "Expected a ReadTransition.";
		}
		if (transition.symbol() == self.input[0]) {
		    self.state = transition.to();
		    return;
		}
	    }
	    self.error_occurred = 1;
	    return;
	} else if (transitions[0] instanceof LALR.DPDA.PushTransition) {
	    if (transitions != 1) {
		throw "Cannot have a push transition and another transition.";
	    }
	    ${ push self.stack, transitions[0].symbol() };
	    self.state = transitions[0].to();
	} else if (transitions[0] instanceof LALR.DPDA.TopTransition) {
	    if (!self.stack) {
		throw "Top transitions require a non-empty stack.";
	    }
	    var top = self.stack[elements(self.stack) - 1];
	    for (var transition in transitions) {
		if (!(transition instanceof LALR.DPDA.TopTransition)) {
		    throw "Expected TopTransition.";
		}
		if (transition.expected() == top) {
		    self.state = transition.to();
		    return;
		}
	    }
	    self.error_occurred = 1;
	} else {
	    throw "Unsupported transition type.";
	}
    }

}

}
}